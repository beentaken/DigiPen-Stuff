// Copyright 2017 DigiPen USA Corporation, All Rights Reserved
// Author: Zero Engine Team
// Edit by: Blake North
// Date Edited: 12/19/2018
// Simple input for a 2d swept character controller.
class SweptInput2D : ZilchComponent
{
  [Dependency] var SweptController : SweptController;
  
  [Property] var LeftKey : Keys = Keys.A;
  [Property] var RightKey : Keys = Keys.D;
  [Property] var JumpKey : Keys = Keys.Space;
  
  [Property]
  var JumpNoise : SoundCue = null;
  
  function Initialize(init : CogInitializer)
  {
    Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
  }

  function OnLogicUpdate(event : UpdateEvent)
  {
    var movement = Real3(0, 0, 0);
    
    // Check for left/right movment
    if (Zero.Keyboard.KeyIsDown(this.LeftKey))
    {
      //made to be += instead of = so that if both are pressed that they cancel them out
      movement.X += -1;
      //Flip sprite
      this.Owner.Sprite.FlipX = true;
    }
    if (Zero.Keyboard.KeyIsDown(this.RightKey))
    {
      //made to be += instead of = so that if both are pressed that they cancel them out
      movement.X += 1;
      //Flip sprite
      this.Owner.Sprite.FlipX = false;
    }
    if (Zero.Keyboard.KeyIsPressed(this.JumpKey))
    {
      this.SweptController.Jump();
      
      if(this.SweptController.Jumping)
      {
        //Used for non looping sound effects
        this.Space.SoundSpace.PlayCue(this.JumpNoise);
      }
    }
    else if (Zero.Keyboard.KeyIsReleased(this.JumpKey))
    {
      this.SweptController.JumpCancel();
    }
    
    this.SweptController.Update(movement, event.Dt);
  }
}
